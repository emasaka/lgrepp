#!/usr/bin/env perl
use strict;
use warnings;
use feature 'say';
use Getopt::Std;
use Encode;
use Encode::Guess;

sub opt2modifier {
    my $opts = shift;
    my $mod = '';
    $mod .= 'i' if ($opts->{i});
    $mod;
}

sub plgrep_file {
    my $conf = shift;
    my $file = shift;

    open my $fh, '<', $file or die;
    my $data = do { local $/; <$fh>; };
    close $fh;

    my $mod = opt2modifier($conf->{opts});
    my $pattern_re = qr/(?$mod)$conf->{pattern}/;

    my $enc = guess_encoding($data, qw(euc-jp shiftjis 7bit-jis));
    my @lines = split /\r?\n/, $enc->decode($data);

    for my $line (@lines) {
        if ($line =~ /$pattern_re/) {
            if ($conf->{numfiles} > 1) {
                print $file, ':';
            }
            say encode('utf-8', $line);
        }
    }
}

sub usage {
    say 'USAGE: plgrep [options] PATTERN [FILE...]';
    exit 1;
}

my %opts;
getopts('i' => \%opts);

my $ptn;
if (@ARGV == 1) {
    $ptn = shift @ARGV;

    my $conf = +{
        pattern => $ptn,
        numfiles => 0,
        opts => \%opts,
    };
    plgrep_file($conf, '/dev/stdin');
} elsif (@ARGV >= 2) {
    $ptn = shift @ARGV;

    my $conf = +{
        pattern => $ptn,
        numfiles => scalar(@ARGV),
        opts => \%opts,
    };
    for my $f (@ARGV) {
        plgrep_file($conf, $f);
    }
} else {
    usage;
}
